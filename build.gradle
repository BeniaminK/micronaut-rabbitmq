buildscript {
    repositories {
        jcenter()
        maven { url  "https://dl.bintray.com/micronaut/core-releases-local" }
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "io.micronaut.build:micronaut-gradle-plugins:2.0.0.RC3"
    }
}

version project.projectVersion

subprojects { Project subproject ->
    group "io.micronaut.configuration"
    apply plugin: "io.micronaut.build.common"
    configurations {
        all {
            resolutionStrategy.eachDependency { DependencyResolveDetails details ->
                String group = details.requested.group
                if(group == 'com.rabbitmq') {
                    details.useVersion(rabbitVersion)
                }
            }
        }
    }

    if (subproject.parent.name == "docs-examples" || subproject.name == "docs-examples") {
        return
    }

    apply plugin: "io.micronaut.build.dependency-updates"
    apply plugin: "io.micronaut.build.publishing"
}

apply from:"https://raw.githubusercontent.com/micronaut-projects/micronaut-docs/v${micronautDocsVersion}/gradle/docs.gradle"